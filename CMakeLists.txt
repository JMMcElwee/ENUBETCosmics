cmake_minimum_required(VERSION 3.12)
project(ENUBETCosmics VERSION 1.0 LANGUAGES CXX)

list(APPEND CMAKE_PREFIX_PATH $ENV{ROOTSYS}
  CMAKE_LIBRARY_OUTPUT_DIRECTORY)

set(CMAKE_EXPORT_COMPILE_COMMANDS 1)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(ECOSMIC_INCLUDE_DIR "${PROJECT_SOURCE_DIR}/include")
set(ECOSMIC_SOURCE_DIR "${PROJECT_SOURCE_DIR}/src")
set(ECOSMIC_TOOLS_DIR "${PROJECT_SOURCE_DIR}/tools")

set(ECOSMIC_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}")
set(ECOSMIC_LIB_DIR "${CMAKE_INSTALL_PREFIX}/lib")
set(ECOSMIC_CMAKECONFIG_DIR "${CMAKE_INSTALL_PREFIX}/lib/cmake/ECosmic")

find_package(ROOT REQUIRED COMPONENTS RIO Core)

include(CMakePackageConfigHelpers)
configure_package_config_file(
  ${PROJECT_SOURCE_DIR}/cmake/ECosmicConfig.cmake.in
  ${CMAKE_CURRENT_BINARY_DIR}/cmake/ECosmicConfig.cmake
  INSTALL_DESTINATION "${ECOSMIC_CMAKECONFIG_DIR}"
  PATH_VARS ECOSMIC_INSTALL_DIR ECOSMIC_CMAKECONFIG_DIR
  )

set(ECOSMIC_LIB_SOURCES
  ${ECOSMIC_SOURCE_DIR}/io.cc
  ${ECOSMIC_SOURCE_DIR}/DBReader.cc
  ${ECOSMIC_SOURCE_DIR}/detector.cc
  ${ECOSMIC_SOURCE_DIR}/EShower.cc
  ${ECOSMIC_SOURCE_DIR}/EParticle.cc
)

add_library(ECosmic SHARED ${ECOSMIC_LIB_SOURCES})
target_include_directories(ECosmic 
  PUBLIC 
    $<BUILD_INTERFACE:${ECOSMIC_INCLUDE_DIR}>
    $<INSTALL_INTERFACE:${ECOSMIC_INSTALL_DIR}/include>
  PRIVATE
    ${ROOT_INCLUDE_DIRS}
  )
target_link_libraries(ECosmic ${ROOT_LIBRARIES})

add_executable(ecorsika ${ECOSMIC_SOURCE_DIR}/ecorsika.cc)
target_include_directories(ecorsika 
  PRIVATE 
  $<BUILD_INTERFACE:${ECOSMIC_INCLUDE_DIR}> 
  $<INSTALL_INTERFACE:${ECOSMIC_INSTALL_DIR}/include> 
  ${ROOT_INCLUDE_DIRS})
target_link_libraries(ecorsika ECosmic ${ROOT_LIBRARIES})


set_target_properties(ECosmic PROPERTIES
  INSTALL_RPATH_USE_LINK_PATH TRUE 
  BUILD_RUNTIME ${CMAKE_CURRENT_BINARY_DIR}
  INSTALL_RUNTIME ${ECOSMIC_LIB_DIR}
)

install(TARGETS ECosmic
  EXPORT ECosmicTargets
  LIBRARY DESTINATION "${ECOSMIC_LIB_DIR}"
  RUNTIME DESTINATION "${ECOSMIC_LIB_DIR}"
)

set_target_properties(ecorsika PROPERTIES
  BUILD_WITH_INSTALL_RPATH TRUE
  INSTALL_RPATH ${ECOSMIC_LIB_DIR}
)

install(TARGETS ecorsika
  EXPORT ECosmicTargets
  LIBRARY DESTINATION "${ECOSMIC_LIB_DIR}"
  RUNTIME DESTINATION "${ECOSMIC_INSTALL_DIR}"
)

install(DIRECTORY ${ECOSMIC_INCLUDE_DIR}
  DESTINATION "${ECOSMIC_INSTALL_DIR}"
  FILES_MATCHING PATTERN "*.hh"
  )

export(TARGETS ECosmic
       FILE "${CMAKE_BINARY_DIR}/cmake/ECosmicTargets.cmake")

install(EXPORT ECosmicTargets
  DESTINATION "${ECOSMIC_CMAKECONFIG_DIR}"
  NAMESPACE ECosmic::
  )

install(FILES ${CMAKE_CURRENT_BINARY_DIR}/cmake/ECosmicConfig.cmake
  DESTINATION ${ECOSMIC_CMAKECONFIG_DIR}
  )

